java {
    sourceCompatibility = project.java_version
    targetCompatibility = project.java_version
}

base {
    archivesName = project.archives_base_name
}

ext {
    commonVmArgs = [
            "-XX:+AllowEnhancedClassRedefinition",
            "-XX:+IgnoreUnrecognizedVMOptions"
    ]

    hotswapVmArgs = [
            "-Dfabric.systemLibraries=${System.getProperty("java.home")}/lib/hotswap/hotswap-agent.jar".toString(),
            "-XX:HotswapAgent=fatjar"
    ]

    commonProperties = [
            "mixin.debug.export": "true",
            "mixin.hotSwap": "true"
    ]

    if (project.hasProperty("username") && project.hasProperty("uuid")) {
        usernameAndUUID = [
                "--username",
                "${username}".toString(),
                "--uuid",
                "${uuid}".toString()
        ]
    }
}

version = project.mod_version
group = project.maven_group

sourceSets.main.resources {
    if (project.hasProperty("use_datagen")) {
        srcDir "src/generated/resources"
    }
}

loom {
    decompilers {
        vineflower {
            options.put("pll", "32767")
            options.put("ind", "    ")
        }
    }
    if (project.hasProperty("use_access_widener")) {
        accessWidenerPath = file("src/main/resources/${archives_base_name}.accesswidener")
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    maven {
        name "ParchmentMC"
        url "https://maven.parchmentmc.org"
        content {
            includeGroup "org.parchmentmc.data"
        }
    }
    maven {
        name "Shedaniel Maven"
        url "https://maven.shedaniel.me"
        content {
            includeGroup "me.shedaniel.cloth"
        }
    }
    maven {
        name "TerraformersMC"
        url "https://maven.terraformersmc.com/releases"
        content {
            includeGroup "com.terraformersmc"
        }
    }
    maven {
        name "DevAuth"
        url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
        content {
            includeGroup "me.djtheredstoner"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchmentmc_version}@zip")
    }

    if (project.hasProperty("fabric_api_version")) {
        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    }
    if (project.hasProperty("modmenu_version")) {
        modImplementation("com.terraformersmc:modmenu:${modmenu_version}") {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
    if (project.hasProperty("cloth_config_version")) {
        modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
    if (rootProject.hasProperty("devauth_version")) {
        modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${rootProject.devauth_version}") {
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
}

jar {
    if (project.hasProperty("use_datagen")) {
        exclude ".cache"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = java_version as int
}

afterEvaluate {
    loom.runs.configureEach {
        vmArg "-javaagent:${configurations.compileClasspath.find { it.name.contains("sponge-mixin") }}"
    }
}