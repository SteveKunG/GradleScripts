buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.modrinth.minotaur:Minotaur:2.+"
    }
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

sourceSets.main.resources {
    if (project.hasProperty("use_datagen")) {
        srcDir "src/generated/resources"
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven {
        name "NeoForge"
        url "https://maven.neoforged.net/releases/"
    }
}

loom {
    if (project.hasProperty("use_access_widener")) {
        neoForge {
            accessTransformers = files("src/main/resources/META-INF/accesstransformer.cfg")
        }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"

    if (project.hasProperty("cloth_config_version")) {
        modImplementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    }
    if (rootProject.hasProperty("devauth_version")) {
        modRuntimeOnly "me.djtheredstoner:DevAuth-neoforge:${devauth_version}"
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }
}

processResources {
    inputs.property "version", mod_version

    filesMatching("META-INF/mods.toml") {
        expand "version": mod_version
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

//TODO Waiting for https://github.com/SpongePowered/Mixin/issues/408 to be fixed
/*afterEvaluate {
    loom.runs.configureEach {
        vmArg "-javaagent:${configurations.compileClasspath.find { it.name.contains("mixin-patched") }}"
    }
}*/

curseforge {
    apiKey = project.hasProperty("curseforge_api_token") ? curseforge_api_token : ""
    project {
        id = curseforge_project_id
        changelog = rootProject.file("CHANGE_LOG.md")
        changelogType = "markdown"
        releaseType = release_type
        addGameVersion "NeoForge"
        addGameVersion minecraft_version

        if (project.hasProperty("curseforge_additional_game_versions") && !curseforge_additional_game_versions.isEmpty()) {
            curseforge_additional_game_versions.split(",").each {
                String gameVersion -> addGameVersion gameVersion
            }
        }

        mainArtifact(remapJar.archiveFile) {
            it.displayName = "[NeoForge ${minecraft_version}] ${display_name} v${mod_version}"
        }

        relations {
            if (project.hasProperty("curseforge_neoforge_required_dependencies") && !curseforge_neoforge_required_dependencies.isEmpty()) {
                curseforge_neoforge_required_dependencies.split(",").each {
                    String dep -> it.requiredDependency dep
                }
            }
            if (project.hasProperty("curseforge_neoforge_optional_dependencies") && !curseforge_neoforge_optional_dependencies.isEmpty()) {
                curseforge_neoforge_optional_dependencies.split(",").each {
                    String dep -> it.optionalDependency dep
                }
            }
        }

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
        addArtifact shadowJar
        addArtifact remapSourcesJar
    }
    options {
        debug = upload_debug.toBoolean()
        forgeGradleIntegration = false
    }
}

import com.modrinth.minotaur.dependencies.ModDependency

modrinth {
    debugMode = upload_debug.toBoolean()
    token = project.hasProperty("modrinth_api_token") ? modrinth_api_token : ""
    projectId = modrinth_project_id
    changelog = rootProject.file("CHANGE_LOG.md").text
    syncBodyFrom = rootProject.file("README.md").text
    versionName = "[NeoForge ${minecraft_version}] ${display_name} v${mod_version}"
    versionNumber = mod_version + "-neoforge"
    versionType = release_type
    uploadFile = remapJar
    gameVersions = ["${minecraft_version}"]
    loaders = ["neoforge"]

    if (project.hasProperty("modrinth_additional_game_versions") && !modrinth_additional_game_versions.isEmpty()) {
        modrinth_additional_game_versions.split(",").each {
            gameVersions.add it
        }
    }
    if (project.hasProperty("modrinth_additional_loaders") && !modrinth_additional_loaders.isEmpty()) {
        modrinth_additional_loaders.split(",").each {
            loaders.add it
        }
    }

    if (project.hasProperty("modrinth_neoforge_required_dependencies") && !modrinth_neoforge_required_dependencies.isEmpty()) {
        modrinth_neoforge_required_dependencies.split(",").each {
            dependencies.add new ModDependency(it, "required")
        }
    }
    if (project.hasProperty("modrinth_neoforge_optional_dependencies") && !modrinth_neoforge_optional_dependencies.isEmpty()) {
        modrinth_neoforge_optional_dependencies.split(",").each {
            dependencies.add new ModDependency(it, "optional")
        }
    }
}