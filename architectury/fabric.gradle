architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

loom {
    if (project.hasProperty("use_access_widener") && use_access_widener.toBoolean()) {
        accessWidenerPath = project(":common").loom.accessWidenerPath
    }
    if (project.hasProperty("use_mixin") && use_mixin.toBoolean()) {
        mixin {
            defaultRefmapName = "mixins.${archives_base_name}_fabric.refmap.json"
        }
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    if (project.hasProperty("fabric_api_version")) {
        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    }
    if (project.hasProperty("modmenu_version")) {
        modApi("maven.modrinth:modmenu:${modmenu_version}") {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
    if (project.hasProperty("cloth_config_version")) {
        modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
            exclude group: "net.fabricmc.fabric-api"
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
    if (rootProject.hasProperty("devauth_version")) {
        modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${rootProject.devauth_version}") {
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowBundle(project(path: ":common", configuration: "transformProductionFabric"))
}

processResources {
    inputs.property "version", mod_version

    filesMatching("fabric.mod.json") {
        expand "version": mod_version
    }
}

afterEvaluate {
    loom.runs.configureEach {
        vmArg "-javaagent:${configurations.compileClasspath.find { it.name.contains("sponge-mixin") }}"
    }
}

curseforge {
    apiKey = project.hasProperty("curseforge_api_token") ? curseforge_api_token : ""
    project {
        id = curseforge_project_id
        changelog = rootProject.file("CHANGE_LOG.md")
        changelogType = "markdown"
        releaseType = release_type
        addGameVersion "Fabric"
        addGameVersion minecraft_version

        if (project.hasProperty("quilt_compatible")) {
            addGameVersion "Quilt"
        }

        if (project.hasProperty("curseforge_additional_game_versions") && !curseforge_additional_game_versions.isEmpty()) {
            curseforge_additional_game_versions.split(",").each {
                String gameVersion -> addGameVersion gameVersion
            }
        }

        mainArtifact(remapJar.archiveFile) {
            it.displayName = "[Fabric ${minecraft_version}] ${display_name} v${mod_version}"
        }

        if (project.hasProperty("curseforge_fabric_required_dependencies") && !curseforge_fabric_required_dependencies.isEmpty()
                || project.hasProperty("curseforge_fabric_optional_dependencies") && !curseforge_fabric_optional_dependencies.isEmpty()
                || project.hasProperty("curseforge_fabric_embedded_dependencies") && !curseforge_fabric_embedded_dependencies.isEmpty()
        ) {
            relations {
                if (project.hasProperty("curseforge_fabric_required_dependencies") && !curseforge_fabric_required_dependencies.isEmpty()) {
                    curseforge_fabric_required_dependencies.split(",").each {
                        String dep -> it.requiredDependency dep
                    }
                }
                if (project.hasProperty("curseforge_fabric_optional_dependencies") && !curseforge_fabric_optional_dependencies.isEmpty()) {
                    curseforge_fabric_optional_dependencies.split(",").each {
                        String dep -> it.optionalDependency dep
                    }
                }
                if (project.hasProperty("curseforge_fabric_embedded_dependencies") && !curseforge_fabric_embedded_dependencies.isEmpty()) {
                    curseforge_fabric_embedded_dependencies.split(",").each {
                        String dep -> it.embeddedLibrary dep
                    }
                }
            }
        }

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        debug = upload_debug.toBoolean()
        forgeGradleIntegration = false
    }
}

modrinth {
    debugMode = upload_debug.toBoolean()
    token = project.hasProperty("modrinth_api_token") ? modrinth_api_token : ""
    projectId = modrinth_project_id
    changelog = rootProject.file("CHANGE_LOG.md").text
    syncBodyFrom = rootProject.file("README.md").text
    versionName = "[Fabric ${minecraft_version}] ${display_name} v${mod_version}"
    versionNumber = mod_version + "-fabric"
    versionType = release_type
    uploadFile = remapJar
    gameVersions = ["${minecraft_version}"]
    loaders = ["fabric"]

    if (project.hasProperty("modrinth_additional_game_versions") && !modrinth_additional_game_versions.isEmpty()) {
        modrinth_additional_game_versions.split(",").each {
            gameVersions.add it
        }
    }
    if (project.hasProperty("quilt_compatible")) {
        loaders.add "quilt"
    }

    dependencies {
        if (project.hasProperty("modrinth_fabric_required_dependencies") && !modrinth_fabric_required_dependencies.isEmpty()) {
            modrinth_fabric_required_dependencies.split(",").each {
                required.project it
            }
        }
        if (project.hasProperty("modrinth_fabric_optional_dependencies") && !modrinth_fabric_optional_dependencies.isEmpty()) {
            modrinth_fabric_optional_dependencies.split(",").each {
                optional.project it
            }
        }
        if (project.hasProperty("modrinth_fabric_embedded_dependencies") && !modrinth_fabric_embedded_dependencies.isEmpty()) {
            modrinth_fabric_embedded_dependencies.split(",").each {
                embedded.project it
            }
        }
    }
}